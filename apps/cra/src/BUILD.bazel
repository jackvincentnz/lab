load("@aspect_rules_jest//jest:defs.bzl", "jest_test")
load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")

ASSET_PATTERNS = [
    "*.svg",
    "*.css",
]

SRC_PATTERNS = [
    "*.tsx",
    "*.ts",
]

# Filename conventions described at
# https://create-react-app.dev/docs/running-tests#filename-conventions
TEST_PATTERNS = [
    "*.test.tsx",
    "*.test.ts",
    "*.spec.tsx",
    "*.spec.ts",
]

js_library(
    name = "src",
    srcs = glob(ASSET_PATTERNS),
    visibility = ["//apps/cra:__pkg__"],
    deps = [
        ":src_ts",
    ],
)

ts_project(
    name = "src_ts",
    srcs = glob(
        include = SRC_PATTERNS,
        exclude = TEST_PATTERNS,
    ),
    declaration = True,
    resolve_json_module = True,
    tsconfig = "//:tsconfig_react",
    deps = [
        "//:node_modules/@types/react",
        "//:node_modules/@types/react-dom",
        "//:node_modules/react",
        "//:node_modules/react-dom",
        "//:node_modules/web-vitals",
    ],
)

ts_project(
    name = "src_test_ts",
    srcs = glob(TEST_PATTERNS),
    declaration = True,
    resolve_json_module = True,
    supports_workers = False,  # worker bug on macos: ERROR: react-cra/src/BUILD.bazel:50:11: output 'src/App.test.js' was not created
    tsconfig = "//:tsconfig_react",
    deps = [
        ":src_ts",
        "//:node_modules/@testing-library/jest-dom",
        "//:node_modules/@testing-library/react",
        "//:node_modules/@types/jest",
        "//:node_modules/@types/testing-library__jest-dom",
    ],
)

jest_test(
    name = "src_test",
    config = "//apps/cra:jest_config",
    data = [
        ":src",
        ":src_test_ts",
        "//:node_modules/jest-environment-jsdom",
        "//:node_modules/jest-transform-stub",
        "//:node_modules/tslib",
    ],
    node_modules = "//:node_modules",
)
