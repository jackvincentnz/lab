load("//tools/bazel/vite:vite.bzl", "vite_build", "vite_dev_server")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("//tools/bazel:js.bzl", "js_library")

js_library(
    name = "codegen",
    srcs = ["codegen.ts"],
    visibility = ["//apps/journal_app/src/__generated__:__pkg__"],
)

SRCS = [
    "public/vite.svg",
    "index.html",
    "package.json",
    "tsconfig.json",
    "vite.config.ts",
]

DEPS = [
    "//apps/journal_app/src",
    "//apps/journal_app/src/__generated__:graphql_codegen",
    "//:node_modules/@apollo/client",
    "//:node_modules/@lab/bubbles",
    "//:node_modules/@mantine/core",  # TODO: could be included by @lab/bubbles?
    "//:node_modules/@tabler/icons-react",  # TODO: could be included by @lab/bubbles?
    "//:node_modules/@vitejs/plugin-react",
    "//:node_modules/react",
    "//:node_modules/react-dom",
    "//:node_modules/vite",
    "//:tsconfig_base",
    "//:tsconfig_react",
]

vite_build(
    name = "build",
    srcs = SRCS + DEPS,
)

vite_dev_server(
    name = "journal_app",
    data = SRCS + DEPS,
)

pkg_tar(
    name = "tar",
    srcs = [":build"],
    # The vite devserver needs to make requests to "base: /journal/" when behind the
    # proxy since the $http_referer can't be used to identify the right app to proxy to.
    # Keep aligned with:
    # - apps/journal_app/vite.config.ts: "base"
    # - infra/local/proxy/nginx.conf: "proxy_pass"
    package_dir = "/usr/share/nginx/html/journal",
    strip_prefix = "dist",
)

oci_image(
    name = "image",
    base = "@nginx_debian_slim",
    tars = [":tar"],
)

oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["lab/journal_app:latest"],
    visibility = ["//:__pkg__"],
)
