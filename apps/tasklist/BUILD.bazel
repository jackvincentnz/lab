load("//tools/bazel/vite:vite.bzl", "vite_build", "vite_dev_server")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("//tools/bazel:js.bzl", "js_library")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")

js_library(
    name = "codegen",
    srcs = ["codegen.ts"],
    visibility = ["//apps/tasklist/src/__generated__:__pkg__"],
)

SRCS = [
    "index.html",
    "package.json",
    "vite.config.ts",
]

DEPS = [
    "//apps/tasklist/src",
    "//apps/tasklist/public",
    "//:node_modules/@vitejs/plugin-react",
    "//:node_modules/vite",
]

vite_build(
    name = "build",
    srcs = SRCS + DEPS,
)

vite_dev_server(
    name = "tasklist",
    data = SRCS + DEPS,
)

pkg_tar(
    name = "tar",
    srcs = [":build"],
    # The vite devserver needs to make requests to "base: /task/" when behind the
    # proxy since the $http_referer can't be used to identify the right app to proxy to.
    # Keep aligned with:
    # - apps/task/vite.config.ts: "base"
    # - infra/local/proxy/nginx.conf: "proxy_pass"
    package_dir = "/usr/share/nginx/html/task",
    strip_prefix = "dist",
)

oci_image(
    name = "image",
    base = "@nginx_alpine_slim",
    tars = [":tar"],
)

write_file(
    name = "tags_tmpl",
    out = "_tags.txt.tmpl",
    content = ["lab/tasklist:VERSION"],
)

expand_template(
    name = "stamped_tags",
    out = "_stamped.tags.txt",
    stamp_substitutions = {"VERSION": "{{STABLE_VERSION}}"},
    substitutions = {"VERSION": "latest"},
    template = "tags_tmpl",
)

oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ":stamped_tags",
    visibility = [
        "//:__pkg__",
        "//apps/organizer/e2e:__pkg__",
    ],
)

write_file(
    name = "version_tmpl",
    out = "_version.txt.tmpl",
    content = ["VERSION"],
)

expand_template(
    name = "stamped_version",
    out = "_stamped.version.txt",
    stamp_substitutions = {"VERSION": "{{STABLE_VERSION}}"},
    substitutions = {"VERSION": "latest"},
    template = "version_tmpl",
)

oci_push(
    name = "push",
    image = ":image",
    remote_tags = ":stamped_version",
    repository = "jackvincent/lab-tasklist",
)
