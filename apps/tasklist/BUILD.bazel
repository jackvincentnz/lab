load("@aspect_rules_js//js:defs.bzl", "js_library", "js_run_devserver")
load("@npm//:react-scripts/package_json.bzl", cra_bin = "bin")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

CRA_DEPS = [
    "//apps/tasklist/src",
    "//apps/tasklist/public",
    "//:node_modules/react-dom",
    "//:node_modules/react-scripts",
    "//:node_modules/react",
    "//:node_modules/typescript",
    "//:node_modules/web-vitals",
    ":package_json",
]

cra_bin.react_scripts(
    # Note: If you want to change the name make sure you update BUILD_PATH below accordingly
    # https://create-react-app.dev/docs/advanced-configuration/
    name = "build",
    srcs = CRA_DEPS,
    args = ["build"],
    chdir = package_name(),
    env = {
        "BUILD_PATH": "./build",
        "DISABLE_ESLINT_PLUGIN": "true",
    },
    out_dirs = ["build"],
)

js_run_devserver(
    name = "start",
    args = ["start"],
    chdir = package_name(),
    command = "../../node_modules/.bin/react-scripts",
    data = CRA_DEPS,
    env = {
        "DISABLE_ESLINT_PLUGIN": "true",
    },
)

js_library(
    name = "package_json",
    srcs = ["package.json"],
    visibility = [":__subpackages__"],
)

js_library(
    name = "codegen",
    srcs = ["codegen.ts"],
    visibility = ["//apps/tasklist/src/__generated__:__pkg__"],
)

alias(
    name = "tasklist",
    actual = ":start",
)

pkg_tar(
    name = "tar",
    srcs = [":build"],
    package_dir = "/usr/share/nginx/html",
    strip_prefix = "build",
)

oci_image(
    name = "image",
    base = "@nginx_debian_slim",
    tars = [":tar"],
)

oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["lab/tasklist:latest"],
    visibility = ["//:__pkg__"],
)
