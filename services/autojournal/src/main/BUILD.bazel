load("@rules_spring//springboot:springboot.bzl", "springboot")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

java_binary(
    name = "main",
    main_class = "nz.geek.jack.autojournal.AutoJournalApplication",
    visibility = [
        "//services/autojournal:__pkg__",
    ],
    runtime_deps = [
        ":main_lib",
    ],
)

springboot(
    name = "main_spring",
    boot_app_class = "nz.geek.jack.autojournal.AutoJournalApplication",
    java_library = ":main_lib",
)

java_library(
    name = "main_lib",
    resources = [
        "//services/autojournal/src/main/resources",
    ],
    runtime_deps = [
        "//services/autojournal/src/main/java/nz/geek/jack/autojournal",
        "@maven//:org_springframework_boot_spring_boot_loader",
        "@maven//:org_springframework_boot_spring_boot_starter_web",
    ],
)

pkg_tar(
    name = "tar",
    srcs = [":main_spring.jar"],
    include_runfiles = True,
    strip_prefix = ".",
)

oci_image(
    name = "image",
    base = "@distroless_java",
    entrypoint = [
        "java",
        "-jar",
        "/main_spring.jar",
    ],
    tars = [":tar"],
)

# Tag and run a local container with:
# $ bazel run //services/autojournal/src/main:tarball
# $ docker run --rm lab/autojournal:latest
oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["lab/autojournal:latest"],
    visibility = [
        "//:__pkg__",
        "//apps/full/e2e:__pkg__",
    ],
)
