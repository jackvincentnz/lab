load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("//tools/bazel/storybook:storybook.bzl", "storybook_build", "storybook_dev_server")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

npm_package(
    name = "bubbles",
    srcs = [
        "package.json",
        "//libs/bubbles/src",
    ],
    # This is a perf improvement; the default will be flipped to False in rules_js 2.0
    include_runfiles = False,
    visibility = ["//visibility:public"],
)

STORYBOOK_SRCS = [
    ".storybook/main.ts",
    ".storybook/preview.tsx",
]

STORYBOOK_DEPS = [
    "//libs/bubbles/src/organisms/shell:stories",
    "//:node_modules/@mantine/core",
    "//:node_modules/@mantine/hooks",
    "//:node_modules/@tabler/icons-react",
    "//:node_modules/@storybook/addon-essentials",
    "//:node_modules/@storybook/addon-interactions",
    "//:node_modules/@storybook/addon-links",
    "//:node_modules/@storybook/blocks",
    "//:node_modules/@storybook/react",
    "//:node_modules/@storybook/react-vite",
    "//:node_modules/@storybook/testing-library",
    "//:node_modules/react",
    "//:node_modules/react-dom",
    "//:node_modules/vite",
]

storybook_build(
    name = "build_storybook",
    srcs = STORYBOOK_SRCS + STORYBOOK_DEPS,
)

storybook_dev_server(
    name = "storybook",
    data = STORYBOOK_SRCS + STORYBOOK_DEPS,
)

pkg_tar(
    name = "tar",
    srcs = [":build_storybook"],
    package_dir = "/usr/share/nginx/html",
    strip_prefix = "storybook-static",
)

oci_image(
    name = "image",
    base = "@nginx_alpine_slim",
    tars = [":tar"],
)

oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["lab/bubbles:latest"],
    visibility = ["//:__pkg__"],
)
